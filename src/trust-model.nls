;; | ======================================================
;; | Filename : trust-model.nls 
;; | Author : Tristan de Blauwe
;; | ======================================================


;; | ========== TRUST COMPONENTS ==========
to-report compute-trust-value [agent]
    let trust-values []
    let wK []
    let coeffs 0

    if(use-it?) [
        let value trust-value-it agent
        if(not empty? value)[
            set trust-values (fput (item 0 value) trust-values)
            set reliability-factors (replace-item 0 reliability-factors (item 1 value))
            set wK (fput (interaction-trust-coeff * (item 1 value)) wK)
            set coeffs coeffs + interaction-trust-coeff
        ]
    ]

    if(use-cr?) [
        let value trust-value-cr agent
        if(not empty? value)[
            set trust-values (fput (item 0 value) trust-values)
            set reliability-factors (replace-item 1 reliability-factors (item 1 value))
            set wK (fput (certified-reputation-coeff * (item 1 value)) wK)
            set coeffs coeffs + certified-reputation-coeff
        ]
    ]

    if(use-wr?) [
        let value trust-value-wr agent
        if(not empty? value)[
            set trust-values (fput (item 0 value) trust-values)
            set reliability-factors (replace-item 2 reliability-factors (item 1 value))
            set wK (fput (witness-reputation-coeff * (item 1 value)) wK)
            set coeffs coeffs + witness-reputation-coeff
        ]
    ]

    if(not empty? trust-values and not empty? wK)[
        let trust-value  (sum (map * wK trust-values)) / (sum wK)
        let reliability  (sum wK) / coeffs
        set reliability-factors (replace-item 3 reliability-factors reliability)

        report (list trust-value reliability)
    ]

    report []
end

;; ========== IT ==========

;; Return the following list (value, reliability) if computable
to-report trust-value-it [agent]
    let _ratings (get-ratings-from agent)
    report (trust-value-from _ratings interaction-trust-reliability)
end

;; ========== CR ==========
;; Same as IT except for the ratings collected
to-report trust-value-cr [agent]
    let _ratings [get-certificates] of agent
    report (trust-value-from _ratings certified-reputation-reliability)
end


;; ========== WR ==========
;; Same as IT except for the ratings collected
to-report trust-value-wr [agent]
    let _ratings (find-witness-ratings (turtle-set self) referral-length-threshold agent)
    report (trust-value-from _ratings witness-reputation-reliability)
end


;; ========== SHARED ==========
to-report weight-function [rating]
    report exp ( ((get-time-from-rating rating) - ticks) / recency-scaling-factor )
end

to-report trust-value-from [_ratings reliability]
    ifelse(length _ratings > 0)[
        let numerateur sum (map [[rating] -> (weight-function rating) * get-value-from-rating rating] _ratings)
        let denominateur sum (map [[rating] -> (weight-function rating)] _ratings)
        let _trust-value numerateur / denominateur
                            
        let deviation-reliability sum (map [[rating] -> (weight-function rating) * abs((get-value-from-rating rating) - _trust-value)] _ratings)

        report (list (_trust-value) ((1 - exp ( - reliability * denominateur)) * (1 - 0.5 * (deviation-reliability / denominateur))))
    ][
        report []
    ]
end

;; | ========== RATINGS ==========
to add-rating [rating]
    let id (get-agent-from-rating rating)
    ifelse (table:has-key? ratings id)[
        let new_ratings (fput rating (table:get ratings id))
        if (length new_ratings > history-size)[set new_ratings but-last new_ratings]
    ][
        table:put ratings id (list rating)
    ]
end

to-report has-ratings-from [agent]
    report table:has-key? ratings ([who] of agent) 
end

;; Returns ratings concerning an agent
to-report get-ratings-from [agent]
    report table:get-or-default ratings ([who] of agent) []
end

to-report get-agent-from-rating [rating]
    report item 0 rating 
end

to-report get-time-from-rating [rating]
    report item 1 rating
end

to-report get-value-from-rating [rating]
    report item 2 rating
end

to-report get-ug-from-rating [rating]
    report (item 2 rating) * 10
end

to-report new-rating [agent value]
    report (list ([who] of agent) ticks (value / 10))
end
